; test for read-over-weakeq interpolants with shared weakpath index
(set-option :produce-proofs true)
(set-option :interpolant-check-mode true)

(set-logic QF_AX)
(declare-sort U 0)
(declare-fun i () U)
(declare-fun j () U)
(declare-fun k1 () U)
(declare-fun k2 () U)
(declare-fun k3 () U)
(declare-fun v1 () U)
(declare-fun v2 () U)
(declare-fun v3 () U)
(declare-fun a () (Array U U))
(declare-fun b () (Array U U))
(declare-fun s1 () (Array U U))
(declare-fun s2 () (Array U U))

(assert (! (and (not (= (select a i) (select b j))) (and (= i j) (and (not (= i k1))
(and (not (= i k3)) (= s1 (store s2 k2 v2)))))) :named A))
(assert (! (and (not (= (select a i) (select b j))) (and (= i j) (and (not (= i k2))
(and (= a (store s1 k1 v1)) (= b (store s2 k3 v3)))))) :named B))

(check-sat)
(set-option :print-terms-cse false)
(get-proof)
(get-interpolants A B)
(exit)