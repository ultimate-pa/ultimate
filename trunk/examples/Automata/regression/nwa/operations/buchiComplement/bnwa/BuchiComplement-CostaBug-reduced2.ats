// 2015-08-15, Matthias Heizmann
// determnistic reduced version of buchiComplement-Bug01.ats
// that reveals bug in 14958 that occurs only in buchiComplementNCSB

NestedLassoWord nlw = [ "c"< >"r" "c"<  "c"< >"r" "c"< , "c"< >"r" "c"< "c"< >"r" >"r" "a" "c"<  ];
boolean accepted = buchiAccepts(ia, nlw);
NestedWordAutomaton complementNCSB = buchiComplementNCSB(ia);
assert(numberOfStates(complementNCSB) == 14);
boolean acceptedComplementNCSB = buchiAccepts(complementNCSB, nlw);
NestedWordAutomaton complementFKV = buchiComplementFKV(ia);
assert(numberOfStates(complementFKV) == 20);
boolean acceptedComplementFKV = buchiAccepts(complementFKV, nlw);
assert(!accepted);
assert(acceptedComplementNCSB);
assert(acceptedComplementFKV);



NestedWordAutomaton ia = (
	callAlphabet = {"c" },
	internalAlphabet = {"a" "b" },
	returnAlphabet = {"r" },
	states = {"s0" "s3" "q0" "s1" "q1" "sX" },
	initialStates = {"q0" },
	finalStates = {"s0" },
	callTransitions = {
		 ("s0" "c" "s1")
		 ("q0" "c" "q1")
		 ("s1" "c" "s1")
		 ("q1" "c" "q1")
	},
	internalTransitions = {
		 ("s3" "a" "s0")
		 ("q0" "a" "q0")
		 ("s1" "a" "s1")
		 ("q1" "a" "q1")
	},
	returnTransitions = {
		 ("s1" "s0" "r" "s3")
		 ("s1" "s1" "r" "s1")
		 ("q1" "q0" "r" "q0")
		 ("q1" "q1" "r" "s0")
	}
);
