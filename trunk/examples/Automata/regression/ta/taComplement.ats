
TreeAutomaton taIntList = TreeAutomaton(
    alphabet = { nil "0" cons succ },
    states = { Num List },
    finalStates = { List },
    transitionTable = {
        ((Num) succ Num)
        (() "0" Num)
        ((Num List) cons List)
        (() nil List)
    }
);


TreeAutomaton taEvenIntList = TreeAutomaton(
    alphabet = { nil "0" cons succ },
    states = { OddNum EvenNum List },
    finalStates = { List },
    transitionTable = {
        ((OddNum) succ EvenNum)
        ((EvenNum) succ OddNum)
        (() "0" EvenNum)
        ((EvenNum List) cons List)
        (() nil List)
    }
);


TreeAutomaton taMul3IntList = TreeAutomaton(
    alphabet = { nil "0" cons succ },
    states = { mod1 mod2 mod0 List },
    finalStates = { List },
    transitionTable = {
        ((mod2) succ mod0)
        ((mod1) succ mod2)
        ((mod0) succ mod1)
        (() "0" mod0)
        ((mod0 List) cons List)
        (() nil List)
    }
);

// [[]]
Tree list0 = Tree [cons(nil,nil)];
// [2, 1]
Tree listA = Tree [cons(succ(succ("0")),cons(succ("0"),nil))];
// [2, 0]
Tree listB = Tree [cons(succ(succ("0")),cons("0",nil))];
// [2, 4]
Tree listC = Tree [cons(succ(succ("0")),cons(succ(succ(succ(succ("0")))),nil))];
// [3, 0]
Tree listD = Tree [cons(succ(succ(succ("0"))),cons("0",nil))];
// [6, 12]
Tree listE = Tree [cons(succ(succ(succ(succ(succ(succ("0")))))),cons(succ(succ(succ(succ(succ(succ(succ(succ(succ(succ(succ(succ("0")))))))))))),nil))];


// Not lists of integers
assert(accepts(complement(taIntList), list0));
assert(accepts(complement(taEvenIntList), list0));
assert(accepts(complement(taMul3IntList), list0));

// taIntList accepts all integer lists
assert(!accepts(complement(taIntList), listA));
assert(!accepts(complement(taIntList), listB));
assert(!accepts(complement(taIntList), listC));
assert(!accepts(complement(taIntList), listD));
assert(!accepts(complement(taIntList), listE));

// taEvenList accepts only lists of even numbers
assert(accepts(complement(taEvenIntList), listA));
assert(!accepts(complement(taEvenIntList), listB));
assert(!accepts(complement(taEvenIntList), listC));
assert(accepts(complement(taEvenIntList), listD));
assert(!accepts(complement(taEvenIntList), listE));

// taMul3IntList accepts only lists of multiples of 3
assert(accepts(complement(taMul3IntList), listA));
assert(accepts(complement(taMul3IntList), listB));
assert(accepts(complement(taMul3IntList), listC));
assert(!accepts(complement(taMul3IntList), listD));
assert(!accepts(complement(taMul3IntList), listE));

// Intersection of taMul3IntList and taEvenIntList accepts only lists of multiples of 6
assert(accepts(complement(intersect(taMul3IntList, taEvenIntList)), listA));
assert(accepts(complement(intersect(taMul3IntList, taEvenIntList)), listB));
assert(accepts(complement(intersect(taMul3IntList, taEvenIntList)), listC));
assert(accepts(complement(intersect(taMul3IntList, taEvenIntList)), listD));
assert(!accepts(complement(intersect(taMul3IntList, taEvenIntList)), listE));

