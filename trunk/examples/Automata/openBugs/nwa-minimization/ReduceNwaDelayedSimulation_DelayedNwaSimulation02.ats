// Author: Matthias Heizmann (heizmann@informatik.uni-freiburg.de)
// Date: 2016-09-26
//
// Automaton consists of two circles (s-circle and p-circle)
// that each use one call and one return.
// In the s-circle the accepting state occurs after the return (in state sf).
// In the p-circle the accepting state occurs after the call (in state pe).
//
// I would presume that each s state can be merged with the corresponding 
// p state. However, the current implementation merges only {s2,p2} 
// and {sx,px}.

NestedWordAutomaton preprocessed = removeUnreachable(nwa);

int minimizeSevpaSize = numberOfStates(minimizeSevpa(preprocessed));
int shrinkNwaSize = numberOfStates(shrinkNwa(preprocessed));
int minimizeNwaPmaxSatDirectSize = numberOfStates(minimizeNwaPmaxSatDirect(preprocessed));
int minimizeNwaPmaxSatDirectSize = numberOfStates(minimizeNwaPmaxSatDirect(preprocessed));
int reduceNwaDirectSimulationSize = numberOfStates(reduceNwaDirectSimulation(preprocessed));
int reduceNwaDirectSimulationBSize = numberOfStates(reduceNwaDirectSimulationB(preprocessed));
int reduceNwaDirectFullMultipebbleSimulationSize = numberOfStates(reduceNwaDirectFullMultipebbleSimulation(preprocessed));
// int reduceNwaDelayedSimulationSize = numberOfStates(reduceNwaDelayedSimulation(preprocessed));
// int reduceNwaDelayedSimulationBSize = numberOfStates(reduceNwaDelayedSimulationB(preprocessed));
// int reduceNwaDelayedFullMultipebbleSimulationSize = numberOfStates(reduceNwaDelayedFullMultipebbleSimulation(preprocessed));
int minimizeNwaPmaxSatDelayedBiSize = numberOfStates(minimizeNwaPmaxSatDelayedBi(preprocessed));

assert(minimizeSevpaSize == 10);
assert(shrinkNwaSize == 8);
assert(minimizeNwaPmaxSatDirectSize == 8);
assert(minimizeNwaPmaxSatDirectSize == 8);
assert(reduceNwaDirectSimulationSize == 8);
assert(reduceNwaDirectSimulationBSize == 8);
assert(reduceNwaDirectFullMultipebbleSimulationSize == 8);
// assert(reduceNwaDelayedSimulationSize == 5);
// assert(reduceNwaDelayedSimulationBSize == 5);
// assert(reduceNwaDelayedFullMultipebbleSimulationSize == 5);
assert(minimizeNwaPmaxSatDelayedBiSize == 5);

NestedWordAutomaton nwa = (
	callAlphabet = {"c"},
	internalAlphabet = {"a"},
	returnAlphabet = {"r"},
	states = {
		"s0" "se" "s2" "sx" "sf"
		"p0" "pe" "p2" "px" "pf"
	},
	initialStates = {"s0" "p0" },
	finalStates = {"sf" "pe" },
	callTransitions = {
		 ("s0" "c" "se")
		 ("p0" "c" "pe")
	},
	internalTransitions = {
		("se" "a" "s2")
		("s2" "a" "sx")
		("pe" "a" "p2")
		("p2" "a" "px")
         
		("sf" "a" "s0")
		("pf" "a" "p0")
	},
	returnTransitions = {
		 ("sx" "s0" "r" "sf")
		 ("px" "p0" "r" "pf")
	}
);
