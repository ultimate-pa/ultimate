(set-option :produce-proofs true)
(set-info :source "{
Test formula which needs AB-mixed interpolators
Desired Interpolant: (and (<= y x1) (implies (>= y x1) (< (f (- x1 5)) x2)))
}")
(set-info :status unsat)
(set-info :difficulty "{ 0 }")
(set-logic AUFLIRA)
(declare-fun a1 () Real)
(declare-fun a2 () Real)
(declare-fun b1 () Real)
(declare-fun b2 () Real)
(declare-fun x1 () Real)
(declare-fun x2 () Real)
(declare-fun y () Real)
(declare-fun f ( Real) Real)
(assert (! (and (<= (* 5.0 a1) x1) (and (= (f a1) a2) (and (<= y a1) (< a2 x2)))) :named IP_0))
(assert (! (and (<= x1 (* 5.0 b1)) (and (= (f b1) b2) (and (<= b1 y) (< x2 b2)))) :named IP_1))
(check-sat)
(get-interpolants IP_0 IP_1)
(exit)
